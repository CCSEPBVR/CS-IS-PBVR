## VERSION
DEFINES += PBVR_VERSION="1.15-UF"
## DETERMINE PLATFORM  ##
win32:PLATFORM=WIN
unix:!macx:PLATFORM=UNX
macx:PLATFORM=MAC
## DETERMINE RELEASE TYPE ##
CONFIG(debug, debug|release) :RELTYPE=debug
CONFIG(release, debug|release) :RELTYPE=release

## INCLUDE PLATFORM SETTINGS ##
include(SETTINGS_$${PLATFORM}.pri)
include(qtpbvr.conf)
## DETERMINE PBVR_MODE MODE ##

equals(PBVR_MODE, IS): DEFINES +=IS_MODE
equals(PBVR_MODE, CS): DEFINES +=CS_MODE
equals(REND_MODE, CPU): DEFINES +=CPU_MODE
equals(REND_MODE, GPU): DEFINES +=GPU_MODE

## WARN ON DEPRECEATED QT FUNC ##
DEFINES += QT_DEPRECATED_WARNINGS
## DISABLE DEPRECEATED QT FUNC (disables all the APIs deprecated before Qt 6.0.0) ##
#DEFINES += QT_DISABLE_DEPRECATED_BEFORE=0x060000

## USE PRECOMPILED HEADERS ##
precompile_header:!isEmpty(PRECOMPILED_HEADER) {
DEFINES += USING_PCH
}

#PBVR INCLUDE PATHS
INCLUDEPATH += ../
INCLUDEPATH += $$(KVS_DIR)/include
INCLUDEPATH += ../Common/$${PBVR_MODE}
INCLUDEPATH += ../FunctionParser

message(KVS_GLEW_DIR :: $${KVS_GLEW_DIR})
!isEmpty(KVS_GLEW_DIR){
    message("KVS_GLEW_DIR in configured. defining KVS_ENABLE_GLEW")
    DEFINES += KVS_ENABLE_GLEW
    INCLUDEPATH += $(KVS_GLEW_DIR)/include
}

message("INCLUDE PATH IS::"$${INCLUDEPATH})
